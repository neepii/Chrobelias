let trace_log fmt = Debug.trace "under" fmt

module type SYM0 = sig
  type term
  type ph

  include FT_SIG.z_term with type term := term

  (* include FT_SIG.z_term with type term := term and  *)
  include FT_SIG.s_ph with type ph := ph and type term := term
  include FT_SIG.s_extra with type ph := ph and type term := term

  val pow2var : string -> term
  val exists : string list -> ph -> ph
end

module type SYM = sig
  include SYM0

  type repr

  val prj : ph -> repr
end

(* TODO(Kakadu): Maybe it's time to use Z.t here  *)
type env = (string, Z.t) Base.Map.Poly.t

let to_normal_env : env -> Env.t =
  Base.Map.Poly.fold ~init:Env.empty ~f:(fun ~key ~data acc ->
    let _ : Env.t = acc in
    let open Ast in
    Env.extend_exn acc (Var (key, I)) (Eia.Const data))
;;

let pp_env ppf env =
  Format.fprintf ppf "@[{|";
  Base.Map.Poly.iteri env ~f:(fun ~key ~data ->
    Format.fprintf ppf "@ @[%s->%a@]@," key Z.pp_print data);
  Format.fprintf ppf " |}@]"
;;

let make_sym (env : env) onvar bound =
  let module M = struct
    include Overapprox.Symantics

    type ph = term
    type repr = term

    let var s =
      match Base.Map.Poly.find env s with
      | None -> Smtml.Expr.symbol (Smtml.Symbol.make_const Smtml.Ty.Ty_int s)
      | Some c -> constz c
    ;;

    let pow2var s = pow (constz (Z.of_int !Config.base)) (var s)
    let prj = Fun.id

    let exists vars x =
      let vars = List.filter (fun s -> Stdlib.not (Base.Map.Poly.mem env s)) vars in
      match vars with
      | [] -> x
      | _ -> Smtml.Expr.exists (List.map var vars) x
    ;;

    let leq l r =
      let open Smtml in
      (* Format.printf
        "Called %s with l = %a and r = %a\n%!"
        __FUNCTION__
        Smtml.Expr.pp
        l
        Smtml.Expr.pp
        r; *)
      Expr.relop Ty.Ty_int Ty.Relop.Le l r
    ;;
  end
  in
  (module struct
    include M
    (* include FT_SIG.Sugar (M) *)
  end : SYM
    with type repr = Smtml.Expr.t)
;;

let make_collector () =
  let module M = struct
    type term = string list
    type str = term
    type ph = term
    type repr = term

    let ( ++ ) = List.append
    let empty = []

    [@@@warning "-32"]

    let str_const _ = empty
    let sofi _ = empty
    let iofs _ = empty
    let str_len _ = empty
    let str_var _ = empty
    let const _ = empty
    let constz _ = empty
    let var _ = empty
    let mul = List.fold_left ( ++ ) []
    let add = List.fold_left ( ++ ) []
    let bw _ = ( ++ )
    let pow = ( ++ )
    let mod_ x _ = x
    let true_ = empty
    let false_ = empty

    (* phormulas  *)
    let in_re _ _ = failwith __FILE__
    let not = Fun.id
    let lor_ = List.fold_left ( ++ ) []
    let land_ = List.fold_left ( ++ ) []
    let eqz = ( ++ )
    let neqz = ( ++ )
    let eq_str = ( ++ )
    let neq_str = ( ++ )
    let lt = ( ++ )
    let leq = ( ++ )
    let prj xs = Base.List.dedup_and_sort xs ~compare:String.compare
    let pow2var x = [ x ]
    let exists vs ivars = List.filter (fun n -> not (List.mem n vs)) ivars
  end
  in
  (module struct
    include M
    include FT_SIG.Sugar (M)
  end : SYM
    with type repr = string list)
;;

exception Bitwise_op
exception String_op
exception Huge_for_smtml of Z.t

let apply_symantics (type a) (module S : SYM with type repr = a) =
  let phs = ref [] in
  let gensym =
    let n = ref 0 in
    fun ?(prefix = "%exp") () ->
      incr n;
      Printf.sprintf "%s%d" prefix !n
  in
  let rec helper = function
    | Ast.Land xs -> S.land_ (List.map helper xs)
    | Lor xs -> S.lor_ (List.map helper xs)
    | Lnot x -> S.not (helper x)
    | True -> S.true_
    | Eia e -> helper_eia e
    | Pred s -> assert false
    | Exists (vs, ph) ->
      let vs =
        List.filter_map
          (function
            | Ast.Any_atom (Ast.Var (s, _)) -> Some s)
          vs
      in
      S.exists vs (helper ph)
    | Unsupp _ -> raise String_op
  and helperT = function
    | Ast.Eia.Const n ->
      (try
         let _ = Z.to_int n in
         S.constz n
       with
       | _ ->
         let basei = !Config.base in
         let base = Z.of_int basei in
         let exp_var = gensym () in
         phs := S.(var exp_var = constz (Z.of_int (Utils.logBaseZ n ~base))) :: !phs;
         S.pow (S.constz base) (S.var exp_var))
    | Atom (Ast.Var (s, _)) -> S.var s
    | Add terms -> S.add (List.map helperT terms)
    | Mul terms -> S.mul (List.map helperT terms)
    | Mod (l, r) -> S.mod_ (helperT l) r
    | Pow (base, p) -> S.pow (helperT base) (helperT p)
    | Bwand _ | Bwor _ | Bwxor _ -> raise Bitwise_op
    | Len _ | Iofs _ | Sofi _ | Concat _ | At _ | Substr _ | Str_const _ | Len2 _ ->
      raise String_op
  and helper_eia eia =
    match eia with
    | Ast.Eia.Eq (l, r, I) -> S.(helperT l = helperT r)
    | Ast.Eia.Neq (l, r, I) -> S.(helperT l <> helperT r)
    | Eq (_, _, S) -> raise String_op
    | Neq (_, _, S) -> raise String_op
    | Leq (l, r) -> S.(helperT l <= helperT r)
    | InRe _ | InReRaw _ | SuffixOf _ | PrefixOf _ | Contains _ | RLen _ ->
      raise String_op
  in
  fun x -> S.prj (S.land_ (helper x :: !phs))
;;

(* Needed for tests, because Z3 gives a model "non-deterministically" *)
let omit_z3_model =
  match Sys.getenv_opt "CHRO_OMIT_Z3_MODEL" with
  | None -> false
  | Some _ -> true
;;

let semenov_bound bound ast =
  let rec compute_bound =
    let open Ast.Eia in
    function
    | Const c -> Z.(max one (abs c))
    | Atom (Var (_, I)) | Pow (_, _) -> Z.one
    | Add terms -> List.fold_left (fun acc x -> Z.(acc + compute_bound x)) Z.one terms
    | Mul terms -> List.fold_left (fun acc x -> Z.(acc * compute_bound x)) Z.one terms
    | term -> Z.one
  in
  Ast.fold
    (fun acc -> function
       | Eia (Eq (lhs, rhs, I)) ->
         max acc Z.(log2 (compute_bound lhs + compute_bound rhs))
       | Eia (Leq (lhs, rhs)) -> max acc Z.(log2 (compute_bound lhs + compute_bound rhs))
       | _ -> acc)
    bound
    ast
;;

let check bound ast =
  try
    let vars = ref (Base.Set.empty (module Base.String)) in
    let interestring_vars =
      let ( ++ ) = List.append in
      Ast.fold
        (fun acc -> function
           | Eia eia ->
             Ast.Eia.fold2
               (fun acc -> function
                  | Ast.Eia.Pow (base, p) ->
                    let _, base_vars, base_vars' = Ast.Eia.collect_lin_exp base in
                    let _, p_vars, p_vars' = Ast.Eia.collect_lin_exp p in
                    let base_vars = base_vars ++ base_vars' in
                    let p_vars = p_vars ++ p_vars' in
                    base_vars ++ p_vars ++ acc
                  | _ -> acc)
               (fun acc _ -> acc)
               acc
               eia
           | _ -> acc)
        []
        ast
      |> Base.Set.Poly.of_list
      |> Base.Set.Poly.to_list
    in
    let bound =
      if bound < 0
      then -1
      else (
        let n, s_bound = List.length interestring_vars, semenov_bound bound ast in
        if Z.(pow (of_int s_bound) n < of_int Config.max_under_const)
        then s_bound
        else if n > 8
        then
          (* With this many exponent variables even a capped sample of the
             choice space is noise, and every candidate re-encodes the whole
             formula into Z3 -- skip straight to the exact engines. *)
          -1
        else Config.config.under_approx)
    in
    let module Map = Base.Map.Poly in
    trace_log "Bound for underapproximation: %d\n" bound;
    trace_log "Interesting: %s\n" (String.concat " " interestring_vars);
    trace_log
      "Expecting %d choices ...\n%!"
      (Utils.pow ~base:bound (List.length interestring_vars));
    let all_choices =
      let ( let* ) xs f = Seq.concat_map f xs in
      let choice1 = Seq.init (bound + 1) Fun.id in
      List.fold_left
        (fun acc name ->
           let* v = choice1 in
           let* acc = acc in
           Seq.return (Map.add_exn acc ~key:name ~data:v))
        (Seq.return Map.empty)
        interestring_vars
    in
    (* The space is enumerated lazily and the first Sat wins, so an
       oversized space still deserves a try -- but a bounded one: with a
       dozen exponent variables the full (bound+1)^n grind is millions of Z3
       calls and starves the rest of the pipeline. *)
    let all_choices = Seq.take (Stdlib.( / ) Config.max_under_const 10) all_choices in
    let exception Early of env in
    let exception Early_Unsat in
    (* Loop-invariant: the choice only contributes the [key = data] conjuncts,
       and the choice space is exponential. *)
    let base_ph = apply_symantics (make_sym Map.empty Fun.id Fun.id) ast in
    try
      Seq.iteri
        (fun i env ->
           let open FT_SIG.To_smtml_symantics in
           let ph =
             Map.fold
               ~init:[ base_ph ]
               ~f:(fun ~key ~data acc -> eqz (var key) (constz (Z.of_int data)) :: acc)
               env
             |> land_
           in
           trace_log "Into Z3 goes: @[%a@]\n%!" Smtml.Expr.pp ph;
           let module Z3 = Smtml.Z3_mappings.Solver in
           (* let module Z3 = Smtml.Cvc5_mappings.Solver in *)
           let solver =
             Z3.make
               ~params:Smtml.Params.(default () $ (Timeout, 60) $ (Random_seed, 42))
               ()
           in
           Z3.reset solver;
           let _ = trace_log "Into Z3 goes: @[%a@]\n%!" Smtml.Expr.pp ph in
           match Z3.check solver ~assumptions:[ ph ] with
           | `Sat when omit_z3_model -> raise (Early (Map.map env ~f:Z.of_int))
           | `Sat ->
             (match Z3.model solver with
              | None -> assert false
              | Some m ->
                let env =
                  Hashtbl.fold
                    (fun k v acc ->
                       let _ : Smtml.Symbol.t = k in
                       match k.name, v with
                       | Smtml.Symbol.Simple s, Smtml.Value.Int n
                         when Bool.not (Map.mem acc s) -> Map.add_exn acc ~key:s ~data:n
                       | _ -> acc)
                    (Smtml.Z3_mappings.values_of_model m)
                    (Map.map env ~f:Z.of_int)
                in
                raise (Early env))
           | `Unsat when List.length interestring_vars == 0 -> raise Early_Unsat
           | _ -> ())
        all_choices;
      (* TODO: if all Unsat, add a constraints (x>bound), becuase we have already checked values in [0.. bound] *)
      let newast =
        let vars = Base.Set.to_list !vars in
        let b = Ast.Eia.Const (Z.of_int bound) in
        let extend v = Ast.eia (Ast.Eia.lt b (Ast.Eia.atom (Ast.Var (v, I)))) in
        Ast.land_ (ast :: List.map extend vars)
      in
      trace_log "Can't decide in %s" __FILE__;
      `Unknown newast
    with
    | Early env ->
      trace_log "%s gives early Sat on %a." __FILE__ Ast.pp_smtlib2 ast;
      (* trace_log "env = %a" pp_env env; *)
      `Sat ("under int", to_normal_env env)
    | Early_Unsat -> `Unsat "nia"
  with
  | String_op | Bitwise_op | Z.Overflow -> `Unknown ast
;;
